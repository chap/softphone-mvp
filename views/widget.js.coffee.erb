# get twilio html (includes token)
jQuery.ajax
  url: 'http<%= 's' unless ENV['RACK_ENV'] == 'development' %>://<%= request.host_with_port %>/widget.html',
  type: 'POST',
  data: window.softphone_options,
  dataType: "html",
  global: false,
  success: (data) ->
    jQuery('body').prepend(data)
    jQuery('#softphone').fadeIn()

    # get twilio js and intitalize widget
    jQuery.getScript "//static.twilio.com/libs/twiliojs/1.1/twilio.min.js", ->
      token = jQuery('#softphone').attr('data-twilio-token')
      Twilio.Device.setup(token, { debug:window.softphone_options.debug_twilio, closeProtection:true })

      Twilio.Device.ready (device) ->
        jQuery("#softphone #log").text("ready")

      Twilio.Device.error (error) ->
        jQuery("#softphone #log").text("error: " + error.message)

      Twilio.Device.connect (conn) ->
        jQuery("#softphone #log").text("successfully established call")
        toggleConnected(true)

      Twilio.Device.disconnect (conn) ->
        jQuery("#softphone #log").text("call ended")
        toggleConnected(false)

      Twilio.Device.incoming (conn) ->
        jQuery("#softphone #log").text("incoming from " + conn.parameters.From)
        jQuery("#softphone #outbound-number").val(conn.parameters.From)
        toggleWidget(true)
        if confirm('Accept incoming call from ' + conn.parameters.From + '?')
          conn.accept()
          toggleConnected(true)
        else
          conn.ignore()

      jQuery('#softphone #call').click ->
        connect_params = {"To": jQuery("#softphone #outbound-number").val()}
        Twilio.Device.connect(connect_params)

      jQuery('#softphone #hangup').click ->
        Twilio.Device.disconnectAll()

      jQuery('#softphone a').toggle(
        ->
          toggleWidget(true)
        , ->
          toggleWidget(false)
      )

      toggleWidget = (direction) ->
        if direction
          jQuery('#softphone').addClass('active').animate({right:'0px'}, 100, ->
            jQuery('#softphone #outbound-number').focus()
          )
        else
          jQuery('#softphone').removeClass('active').animate({right:'-280px'}, 100)

      toggleConnected = (status) ->
        if status
          jQuery('#softphone #call').attr("disabled", true)
          jQuery('#softphone #hangup').removeAttr("disabled")
        else
          jQuery('#softphone #call').removeAttr("disabled")
          jQuery('#softphone #hangup').attr("disabled", true)

      # link phone numbers
      if window.softphone_options.number_selectors
        jQuery(window.softphone_options.number_selectors).each (i,e) ->
          jQuery(e).replaceWith(
            "<a href='#' class='softphone'>" + jQuery(e).text() + "</a>"
          )

      jQuery('a.softphone').click (e) ->
        e.preventDefault()
        toggleWidget(true)
        jQuery('#softphone #outbound-number').val jQuery(this).text()
