# get twilio html (includes token)
jQuery.ajax
  url: '//<%= request.host_with_port %>/widget.html',
  type: 'POST',
  data: window.softphone_options,
  dataType: "html",
  global: false,
  success: (data) ->
    jQuery('body').append(data)
    jQuery('#softphone').fadeIn()

    # get twilio js and intitalize widget
    jQuery.getScript "//static.twilio.com/libs/twiliojs/1.1/twilio.min.js", ->
      token = jQuery('#softphone').attr('data-twilio-token')
      Twilio.Device.setup(token, { debug:window.softphone_options.debug_twilio })

      Twilio.Device.ready (device) ->
        jQuery("#softphone #log").text("ready")

      Twilio.Device.error (error) ->
        jQuery("#softphone #log").text("error: " + error.message)

      Twilio.Device.connect (conn) ->
        jQuery("#softphone #log").text("successfully established call")

      Twilio.Device.disconnect (conn) ->
        jQuery("#softphone #log").text("call ended")

      Twilio.Device.incoming (conn) ->
        jQuery("#softphone #log").text("incoming from " + conn.parameters.From)
        #accept the incoming connection and start two-way audio
        conn.accept()

      jQuery('#softphone #call').click ->
        connect_params = {"PhoneNumber": jQuery("#softphone #outbound-number").val()}
        Twilio.Device.connect(connect_params)

      jQuery('#softphone #hangup').click ->
        Twilio.Device.disconnectAll()

      jQuery('#softphone a').toggle(
        ->
          jQuery('#softphone').animate({right:'0px'}, 100, ->
            jQuery('#softphone #outbound-number').focus()
          )
        , ->
          jQuery('#softphone').animate({right:'-266px'}, 100)
      )

      # link all phone numbers
      if window.softphone_options.number_regex
        number_regex = new RegExp window.softphone_options.number_regex, 'g'
        jQuery('#customerSummary').html(
          jQuery('#customerSummary').html().replace(
            number_regex,
            '<a href="#" class="softphone">$1</a>'
          )
        )

